// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    // NOTE: xxx.yy.zz; -SNAPSHOT; -RCxx
    VERSION_NAME = '0.3.7'
    SDK_VERSION = 33
    MIN_SDK_VERSION = 19
    BUILD_TOOL_VERSION = '33.0.1'

    multidexVersion = '2.0.1'
    androidxDrawerVersion = '1.2.0'
    androidxPrefVersion = '1.2.1'
    androidxLifecycleVersion= '2.5.1'
    materialVersion = '1.9.0'
    //appcompatVersion = '1.4.1'
    guavaVersion = '31.0.1'
    daggerVersion = '2.43.2'
    javaxAnnotationVersion = '1.3.2'
    rxjavaVersion = '2.2.12'
    rxandroidVersion = '2.1.1'
    nextcloudLibraryVersion = '1.5.0'
    jsoupVersion = '1.14.3'
    stethoVersion = '1.6.0'
    // Test
    testCoreVersion = '1.5.0'
    junitVersion = '4.13.2'
    mockitoVersion = '4.3.1'
    // AndroidTest
    espressoVersion = '3.5.1'
    testRunnerVersion = '1.5.2'
    testRulesVersion = '1.5.0'
    testExtVersion = '1.1.5'
    dexmakerVersion = '2.28.1'
    // Resolve conflicts
    //findbugsJsr305Version = '3.0.2'
}

ext {
    buildVersionCode = {
        // versionCode MAX: 2 100 000 000
        // Release: xxx.yy.zz.99
        // -SNAPSHOT: *.00
        // -RCxx: *.(1-98)
        def candidate = '99'
        def (major, minor, patch) = VERSION_NAME.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = '0'
            patch = patch.replaceAll('[^0-9]', '')
        } else {
            def rc
            (patch, rc) = patch.tokenize('rc')
            if (rc) {
                candidate = rc
            }
        }
        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect{it.toInteger()}
        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate
    }
}

buildscript {
    ext.kotlin_version = '1.8.10'
    ext.core_ktx_version = '1.10.1'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        // https://jitpack.io/docs/BUILDING/#gradle-projects
        // https://jitpack.io/com/github/USER/REPO/TAG/build.log
        maven { url 'https://jitpack.io' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    /*gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }*/
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
