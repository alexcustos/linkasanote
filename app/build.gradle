apply plugin: 'com.android.application'

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId "com.bytesforge.linkasanote"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion SDK_VERSION
        versionCode buildVersionCode()
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "ru-rRU"
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
    }

    /**
     * To sign release build, create the file release-signing.properties with the follow content:
     *
     * signingStoreLocation=/path/to/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    File releaseSigning = project.rootProject.file('release-signing.properties')
    if (releaseSigning.exists()) {
        Properties signingProperties = new Properties()
        releaseSigning.withInputStream { signingProperties.load(it) }

        // NOTE: signingProperties.hasProperty is not working
        println "Found release-signing.properties, will try to sign the build..."
        signingConfigs {
            release {
                storeFile file(signingProperties.getProperty('signingStoreLocation'))
                storePassword signingProperties.getProperty('signingStorePassword')
                keyAlias signingProperties.getProperty('signingKeyAlias')
                keyPassword signingProperties.getProperty('signingKeyPassword')
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    lintOptions {
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }

    flavorDimensions "mode"
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod
    }

    packagingOptions {
        pickFirst 'androidsupportmultidexversion.txt'
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/README'
        exclude 'META-INF/README.txt'
        exclude 'META-INF/README.md'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/*.*.version'
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            showStandardStreams = true
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':androidutils')
    implementation 'com.github.alexcustos:TokenAutoComplete:2.1.4'

    //implementation "com.android.support:multidex:${multidexVersion}"
    //implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    //implementation "com.android.support:design:${supportLibraryVersion}"
    //implementation "com.android.support:preference-v14:${supportLibraryVersion}"
    //implementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    //implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    //implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    implementation "com.google.guava:guava:${guavaVersion}-android"
    implementation "com.github.nextcloud:android-library:${nextcloudLibraryVersion}"
    implementation "org.jsoup:jsoup:${jsoupVersion}"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxandroidVersion}"

    // AndroidX
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.test.ext:junit:1.1.3'

    // Dagger 2
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    //implementation "com.google.dagger:dagger-android:${daggerVersion}"
    //implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    compileOnly "javax.annotation:jsr250-api:${jsr250ApiVersion}"

    // Stetho
    implementation "com.facebook.stetho:stetho:${stethoVersion}"

    // Local unit tests
    testImplementation "androidx.test:core:${coreVersion}";
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    // Espresso Testing
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation "androidx.test:runner:${testVersion}"
    androidTestImplementation "androidx.test:rules:${testVersion}"
    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:${dexmakerVersion}"
    //androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:${uiAutomatorVersion}"

    // Resolve conflicts
    implementation "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
    //androidTestImplementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    //androidTestImplementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    //androidTestImplementation "com.android.support:design:${supportLibraryVersion}"
}
